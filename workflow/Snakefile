# ToDo: 
# Start from NYGC vcfs
# Option to specify chromsomes (process all separately)
# A haplotag on the ONT reads would be great for visualization

configfile: "Snake.config.yaml"

samtools_bin = "/g/korbel/hoeps/anaconda_2022/miniconda3/envs/whatshap-env/bin/samtools"
whatshap_bin = "/g/korbel/hoeps/anaconda_2022/miniconda3/envs/whatshap-env/bin/whatshap"
bcftools_bin = "/g/korbel/hoeps/anaconda_2022/miniconda3/envs/ashleys/bin/bcftools"
bgzip_bin = '/g/korbel/hoeps/anaconda_2022/miniconda3/envs/whatshap-env/bin/bgzip'
tabix_bin = '/g/korbel/hoeps/anaconda_2022/miniconda3/envs/whatshap-env/bin/tabix'
bedtools_bin = '/g/korbel/hoeps/anaconda_2022/miniconda3/envs/whatshap-env/bin/bedtools'


sseq_dir = config['path_to_sseq_bamdir']
ont_dir = config['path_to_longread_bamdir']

# Input: 
# Sseq Cell(s)
# List of chromosomes

CHROMS = ['chr' + str(i) for i in range(1, 23)]
#CHROMS = ['chr' + str(i) for i in range(20,22)]
SSEQ_CELL = ['NW130711.291.L008.srt.mdup', 
            'NW150212-III.33.L001.srt.mdup',
            'NW150212-III.35.L001.srt.mdup',
            'NW150212-III.36.L001.srt.mdup',
            'NW150212-III.39.L001.srt.mdup',
            'NW150212-III.42.L001.srt.mdup',
            'NW150212-III.45.L001.srt.mdup',
            'NW150212-III.48.L001.srt.mdup',
            'NW150212-III.51.L001.srt.mdup',
            'NW150212-III.52.L001.srt.mdup']
SAMPLE = 'NA12878'


rule all: 
    input:
        #expand('res/{sample}/{sseq_cell}/{chr}/strong_res/strong_reads_haplotagged.bam', sample=SAMPLE, sseq_cell = SSEQ_CELL, chr = CHROMS),
        #expand('res/{sample}/{sseq_cell}/{chr}/strong_res/strong_reads_haplotagged.bam.bai', sample=SAMPLE, sseq_cell = SSEQ_CELL, chr = CHROMS),
        #expand('res/{sample}/strong-all/{sseq_cell}_strong.bam', sample=SAMPLE, sseq_cell=SSEQ_CELL),
        #expand('res/{sample}/strong-all/{sseq_cell}_strong.bam.bai', sample=SAMPLE, sseq_cell=SSEQ_CELL),
        expand('res/{sample}/strong-mini/{sseq_cell}_strong_mini.bed', sample=SAMPLE, sseq_cell='NW150212-III.42.L001.srt.mdup')


rule get_onehap_strong_mini_one_chr:
    input:
        sseq_onehap = 'res/{sample}/{sseq_cell}/{chr}/sseq_split/sseq_h{haplotype}.bam',
        strong_all = 'res/{sample}/strong-all/{sseq_cell}_strong.bam'
    output:
        'res/{sample}/{sseq_cell}/{chr}/strong-mini-h{haplotype}/{sseq_cell}_strong_mini.bed'
    shell:
        """
        bash scripts/find_longest_ont_per_sseq.sh {input.sseq_onehap} {input.strong_all} {wildcards.chr} {wildcards.haplotype} {output} {samtools_bin} {bedtools_bin}
        """


rule combine_chr_strong_mini_into_one:
    input:
        expand('res/{{sample}}/{{sseq_cell}}/{chr}/strong-mini/{{sseq_cell}}_strong_mini.bed', chr=CHROMS),
    output:
        unsrt = 'res/{sample}/strong-mini/{sseq_cell}_strong_mini_unsrt.bed',
        srt = 'res/{sample}/strong-mini/{sseq_cell}_strong_mini.bed'
    shell:
        """
        cat {input} > {output.unsrt}
        {bedtools_bin} sort -i {output.unsrt} > {output.srt}
        """

rule one_bed_mini_combine:
    input:
        h1_strong_mini = 'res/{sample}/{sseq_cell}/{chr}/strong-mini-h1/{sseq_cell}_strong_mini.bed',
        h2_strong_mini = 'res/{sample}/{sseq_cell}/{chr}/strong-mini-h2/{sseq_cell}_strong_mini.bed',
        #hx_strong_mini = 'res/{sample}/{sseq_cell}/{chr}/strong-mini-hx/{sseq_cell}_strong_mini.bed'
    output:
        "res/{sample}/{sseq_cell}/{chr}/strong-mini/{sseq_cell}_strong_mini.bed"
    shell:
        """
        cat {input.h1_strong_mini} {input.h2_strong_mini} > {output}
        """


rule merge_bams:
    input:
        expand('res/{{sample}}/{{sseq_cell}}/{chr}/strong_res/strong_reads_haplotagged.bam', chr = CHROMS)
    output:
        'res/{sample}/strong-all/{sseq_cell}_strong.bam'
    shell:
        """
        {samtools_bin} merge {output} {input}
        {samtools_bin} index {output}
        """

rule turn_seqq_bam_into_one_chr:
    input:
        bam = '{}/{{sseq_cell}}.bam'.format(sseq_dir)
    output: 
        bam_onechr = 'res/{sample}/{sseq_cell}/{chr}/sseq_split.bam'
    params:
        mapq_th = '60'
    shell:
        """
        # -q {params.mapq_th}: filters out reads with a MAPQ score lower than the threshold defined by {params.mapq_th}
        # -f 2: keeps only reads that are properly paired (both reads of the pair are correctly oriented with expected insert size)
        # -F 4: filters out reads that are not mapped
        # -F 8: filters out reads where the mate is not mapped
        # -F 256: filters out secondary alignments (keeps only the primary alignments)
        {samtools_bin} view -b -q {params.mapq_th} -f 2 -F 4 -F 8 -F 256 {input.bam} {wildcards.chr} > {output.bam_onechr}
        """

rule turn_ont_bam_into_one_chr:
    input:
        bam = '{}/{{sample}}.bam'.format(ont_dir)
    output: 
        bam_onechr = 'res/{sample}/ont_bams/{chr}/ont_split.bam'
    params:
        mapq_th = '60'
    shell:
        """
        {samtools_bin} view -b -q {params.mapq_th} -F 4 {input.bam} {wildcards.chr} > {output.bam_onechr}
        """

rule obtain_het_snp_vcf:
    output:
        "res/{sample}/het_vcfs/{chr}.vcf.gz"
    params:
        snp_file_dir = config['snp_file_dir'],
        ExcHet_th = '0.1'
    shell:
        """
        {bcftools_bin} view -s {wildcards.sample} -i 'INFO/ExcHet > {params.ExcHet_th}' --exclude-types indels {params.snp_file_dir}/1kGP_high_coverage_Illumina.{wildcards.chr}.filtered.SNV_INDEL_SV_phased_panel.vcf.gz | grep -e "^#" -e "GT\t0|1" -e "GT\t1|0" | {bgzip_bin} > {output} 
        """

rule split_sseq_h1_h2:
    input:
        sseq_cell = "res/{sample}/{sseq_cell}/{chr}/sseq_split.bam", 
        sseq_cell_idx = "res/{sample}/{sseq_cell}/{chr}/sseq_split.bam.bai", 
        snp_file = "res/{sample}/het_vcfs/{chr}.vcf.gz",
        snp_file_idx = "res/{sample}/het_vcfs/{chr}.vcf.gz.tbi"
    output:
        sseq_h1 = "res/{sample}/{sseq_cell}/{chr}/sseq_split/sseq_h1.bam",
        sseq_h2 = "res/{sample}/{sseq_cell}/{chr}/sseq_split/sseq_h2.bam",
        haplotag_list = 'res/{sample}/{sseq_cell}/{chr}/sseq_split/haplotags.tsv'
    params:
        ref_fa = config['ref_fa'] 
    shell:
        """
        {whatshap_bin} haplotag \
                        -o res/{wildcards.sample}/{wildcards.sseq_cell}/{wildcards.chr}/tmp.bam \
                       --reference {params.ref_fa} \
                       {input.snp_file} \
                       {input.sseq_cell} \
                       --ignore-read-groups \
                       --skip-missing-contigs \
                       --output-haplotag-list {output.haplotag_list}

        {whatshap_bin} split \
                        --output-h1 {output.sseq_h1} \
                        --output-h2 {output.sseq_h2} \
                        {input.sseq_cell} \
                        {output.haplotag_list}

        rm res/{wildcards.sample}/{wildcards.sseq_cell}/{wildcards.chr}/tmp.bam
        """


rule split_sseq_hx_plus_minus:
    input:
        in_bam = "res/{sample}/{sseq_cell}/{chr}/sseq_split/sseq_{hap}.bam"
    output:
        out_bam = "res/{sample}/{sseq_cell}/{chr}/sseq_split/sseq_{hap}_{direction}.bam"
    shell:
        """
        bash scripts/split_bam_by_direction_of_first_read.sh {input.in_bam} {output.out_bam} {wildcards.direction} {samtools_bin}
        """


rule split_longreads_h1_h2:
    input:
        lr_bam = 'res/{sample}/ont_bams/{chr}/ont_split.bam', 
        lr_bam_idx = 'res/{sample}/ont_bams/{chr}/ont_split.bam.bai', 
        snp_file = "res/{sample}/het_vcfs/{chr}.vcf.gz",
        snp_file_idx = "res/{sample}/het_vcfs/{chr}.vcf.gz.tbi"
    output:
        lr_h1 = 'res/{sample}/{sseq_cell}/{chr}/lr_split/lr_h1.bam',
        lr_h2 = 'res/{sample}/{sseq_cell}/{chr}/lr_split/lr_h2.bam',
        haplotag_list = 'res/{sample}/{sseq_cell}/{chr}/lr_split/haplotags.tsv'
    params:
        ref_fa = config['ref_fa'] 
    shell: 
        """
        {whatshap_bin} haplotag \
                        -o res/{wildcards.sample}/{wildcards.sseq_cell}/{wildcards.chr}/tmp2.bam \
                       --reference {params.ref_fa} \
                       {input.snp_file} \
                       {input.lr_bam} \
                       --ignore-read-groups \
                       --skip-missing-contigs \
                       --tag-supplementary \
                       --output-haplotag-list {output.haplotag_list}

        {whatshap_bin} split \
                        --output-h1 {output.lr_h1} \
                        --output-h2 {output.lr_h2} \
                        {input.lr_bam} \
                        {output.haplotag_list}

        rm res/{wildcards.sample}/{wildcards.sseq_cell}/{wildcards.chr}/tmp2.bam
        """

rule make_eight_plusify_minusify:
    input:
        sseq_bam = "{foo}/sseq_split/sseq_{hap}_{direction}.bam",
        lr_bam =   "{foo}/lr_split/lr_{hap}.bam"
    output:
        tmp_file = '{foo}/lr_sseq_eightfiles/TEMP_lr_h{hap}_{direction}.tsv',
        plusify =  '{foo}/lr_sseq_eightfiles/lr_h{hap}_{direction}_plusify.bam',
        minusify = '{foo}/lr_sseq_eightfiles/lr_h{hap}_{direction}_minusify.bam'
    params:
        # Provide the correct truth value for each direction
        plusify_truth_value = lambda wildcards: 'true' if wildcards.direction == 'plus' else 'false',
        minusify_truth_value = lambda wildcards: 'false' if wildcards.direction == 'plus' else 'true'
    shell:
        """
        bash scripts/overlap_ont_sseq.sh {input.lr_bam} {input.sseq_bam} {output.tmp_file} {bedtools_bin}

        bash scripts/filter_bam_with_list_directionspecific.sh {input.lr_bam} {output.plusify} {output.tmp_file} {params.plusify_truth_value} {samtools_bin}
        bash scripts/filter_bam_with_list_directionspecific.sh {input.lr_bam} {output.minusify} {output.tmp_file} {params.minusify_truth_value} {samtools_bin}
        """ 



rule flip_one_plusify_or_minusify_bam:
    input: 
        '{foo}/lr_sseq_eightfiles/lr_h{hap}_{direction}_{operation}.bam'
    output:
        output_tmp = '{foo}/lr_sseq_eightfiles_reorient/lr_h{hap}_{direction}_{operation}_executed.bam',
        output =     '{foo}/lr_sseq_eightfiles_reorient/lr_h{hap}_{direction}_{operation}_executed_sort.bam'
    shell:
        """
        bash scripts/modify_bam_orientation.sh {input} {output.output_tmp} {wildcards.operation} {samtools_bin}
        {samtools_bin} sort -o {output.output} {output.output_tmp}

        """

rule merge_eight_into_one:
    input:
        expand('{{foo}}/lr_sseq_eightfiles_reorient/lr_h{hap}_{direction}_{operation}_executed_sort.bam', 
        hap = ["h1", "h2"], direction = ["plus", "minus"], operation = ["plusify", "minusify"])
    output:
        '{foo}/strong_res/strong_reads.bam'
    shell:
        """
        {samtools_bin} merge {output} {input}
        """

rule haplotag_final_bam:
    input:
        reads_bam = 'res/{sample}/{sseq_cell}/{chr}/strong_res/strong_reads.bam',
        reads_bam_bai = 'res/{sample}/{sseq_cell}/{chr}/strong_res/strong_reads.bam.bai',
        phased_snp_vcf = "res/{sample}/het_vcfs/{chr}.vcf.gz"
    output:
       'res/{sample}/{sseq_cell}/{chr}/strong_res/strong_reads_haplotagged.bam'
    params:
        ref_fa = config['ref_fa'] 
    shell: 
        """
        {whatshap_bin} haplotag \
                        -o {output} \
                       --reference {params.ref_fa} \
                       {input.phased_snp_vcf} \
                       {input.reads_bam} \
                       --ignore-read-groups \
                       --skip-missing-contigs \
                       --tag-supplementary
        """



# General rules that should be fine regardless of context

rule index_bam:
    input: 
        '{foo}.bam'
    output:
        '{foo}.bam.bai'
    shell:
        """
        {samtools_bin} index {input}
        """

rule compress_vcf:
    input:
        '{foo}.vcf'
    output:
        '{foo}.vcf.gz',
    shell:
        """
        {bgzip_bin} -c {input} > {output}
        """
   
rule index_vcf:
    input:
        '{foo}.vcf.gz'
    output:
        '{foo}.vcf.gz.tbi'
    shell:
        """
        {tabix_bin} -p vcf {input}
        """
